plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'net.william278'
version = '1.0'

var jettyServerVersion = '11.0.8'
var jettyServletVersion = '11.0.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'minebench'
        url = 'https://repo.minebench.de/'
    }
}

shadowJar {
    relocate 'de.themoep', 'net.william278.schematicupload.libraries'
    relocate 'org.bstats', 'net.william278.schematicupload.libraries.bstats'

    dependencies {
        //noinspection GroovyAssignabilityCheck
        exclude dependency(':slf4j-api')
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    // These versions MUST match the ones in plugin.yml
    compileOnly 'org.eclipse.jetty:jetty-server:' + jettyServerVersion
    compileOnly 'org.eclipse.jetty:jetty-servlet:' + jettyServletVersion

    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'de.themoep:minedown:1.7.1-SNAPSHOT'
}

jar.dependsOn shadowJar

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version, jettyServerVer: jettyServerVersion, jettyServletVer: jettyServletVersion]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
